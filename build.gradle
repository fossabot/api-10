buildscript {
    ext {
        kotlinVersion = '1.2.30'
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-noarg'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'


group = 'com.dongfg.project'
version = '2.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    snippetsDir = file('build/generated-snippets')
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

test {
    outputs.dir snippetsDir
    include '**/ApiDocumentation.class'
}

noArg {
    annotation("org.springframework.boot.context.properties.ConfigurationProperties")
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    archiveName 'dongfg-api.jar'
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')
    compile("com.fasterxml.jackson.module:jackson-modules-java8:2.9.4")
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")

    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile('org.springframework.boot:spring-boot-starter-undertow')
    compile('org.springframework.boot:spring-boot-starter-quartz') {
        exclude group: 'com.zaxxer', module: 'HikariCP-java6'
    }
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('mysql:mysql-connector-java')

    compile('com.graphql-java:graphql-spring-boot-starter:4.0.0')
    compile('com.graphql-java:graphiql-spring-boot-starter:4.0.0')
    compile('com.graphql-java:graphql-java-tools:4.3.0')

    compile('io.github.microutils:kotlin-logging:1.4.9')
    compile('com.yunpian.sdk:yunpian-java-sdk:1.2.6')
    compile('com.warrenstrange:googleauth:1.1.2')

    compile('org.springframework.boot:spring-boot-starter-actuator')
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')
    compile('javax.xml.bind:jaxb-api:2.3.0')
    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.0.RELEASE'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:2.0.0.RELEASE'
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.projectreactor:reactor-test')
}
