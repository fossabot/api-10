buildscript {
    ext {
        kotlinVersion = '1.2.31'
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-noarg'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'


group = 'com.dongfg.project'
version = '2.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }

    doLast {
        def metadata = copySpec {
            from 'build/tmp/kapt3/classes/main/META-INF/spring-configuration-metadata.json'
        }
        copy {
            with metadata
            into 'out/production/classes/META-INF/'
        }
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

noArg {
    annotation("org.springframework.boot.context.properties.ConfigurationProperties")
    invokeInitializers = true
}

bootJar {
    archiveName 'dongfg-api.jar'
}



repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')
    compile("com.fasterxml.jackson.module:jackson-modules-java8:2.9.4")
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")

    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile('org.springframework.boot:spring-boot-starter-undertow')
    compile('org.springframework.boot:spring-boot-starter-quartz') {
        exclude group: 'com.zaxxer', module: 'HikariCP-java6'
    }
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('io.jsonwebtoken:jjwt:0.9.0')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('mysql:mysql-connector-java')
    compile('org.springframework.session:spring-session-data-mongodb')

    compile('io.springfox:springfox-swagger2:2.8.0')
    compile('io.springfox:springfox-swagger-ui:2.8.0')

    compile('com.graphql-java:graphql-spring-boot-starter:4.0.0')
    compile('com.graphql-java:graphiql-spring-boot-starter:4.0.0')
    compile('com.graphql-java:graphql-java-tools:4.3.0')

    compile('io.github.microutils:kotlin-logging:1.4.9')
    compile('com.yunpian.sdk:yunpian-java-sdk:1.2.6')
    compile('com.warrenstrange:googleauth:1.1.2')
    compile('com.google.guava:guava:24.1-jre')

    kapt('org.springframework.boot:spring-boot-configuration-processor')
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')
    compile('javax.xml.bind:jaxb-api:2.3.0')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.projectreactor:reactor-test')
}
